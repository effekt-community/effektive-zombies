module ffi

extern type Event

extern def addEventListener(eventType: String, callback: Event => Unit at {io, global}) at io: Unit =
  jsWeb """document.defaultView.addEventListener(${eventType}, 
           (event) => $effekt.runToplevel((ks, k) => ${callback}(event, ks, k)))"""

extern def getKey(event: Event) at {}: String =
  jsWeb "${event}.key"

extern def getClientX(event: Event) at {}: Double = 
  jsWeb "${event}.clientX"

extern def getClientY(event: Event) at {}: Double =
  jsWeb "${event}.clientY"

extern def getButton(event: Event) at {}: Int =
  jsWeb "${event}.button"

extern type Context2D

extern jsWeb """
  function get$canvas() {
    return document.getElementById("canvas")
  }

  function $resize(canvas, width, height) {
    canvas.style.width = width + "px";
    canvas.style.height = height + "px";
    ctx = canvas.getContext("2d");
    ctx.imageSmoothingEnabled = true;
    return ctx;
  }
"""

extern def createCanvas(width: Int, height: Int) at io: Context2D =
  jsWeb """(function(){
  canvas = document.createElement("canvas");
  canvas.id = "canvas";
  canvas.width = ${width};
  canvas.height = ${height};
  //https://stackoverflow.com/a/7782251/15982248
  canvas.style.position = "absolute";
  canvas.style.top = '50%';
  canvas.style.left = '50%';
  canvas.style.transform = 'translate(-50%, -50%)';
  canvas.style.setProperty("image-rendering", "pixelated");
  $resize(canvas, ${width}, ${height});
  document.body.appendChild(canvas);
  return ctx;
  })()
  """

///https://www.youtube.com/watch?v=1y57COMRSdA
extern def resizeCanvas(width: Int, height: Int) at io: Context2D =
  jsWeb """(function(){
  canvas = get$canvas();
  return $resize(canvas, ${width}, ${height});
  })()
  """

extern def drawRect(
  ctx: Context2D, x: Int, y: Int, 
  width: Int, height: Int, color: String
) at io: Unit =
  jsWeb """${ctx}.fillStyle = ${color}; 
           ${ctx}.fillRect(${x}, ${y}, ${width}, ${height});"""

extern def drawCircle(
  ctx: Context2D, x: Int, y: Int, 
  radius: Int, color: String
) at io: Unit =
  jsWeb """${ctx}.fillStyle = ${color};
           ${ctx}.beginPath();
           ${ctx}.arc(${x}, ${y}, ${radius}, 0, 2 * Math.PI);
           ${ctx}.fill();"""

extern def clear(ctx: Context2D) at io: Unit = 
  jsWeb """canvas = get$canvas();
           w = canvas.width;
           h = canvas.height;
           ${ctx}.clearRect(0, 0, w, h)"""

extern def drawText(
  ctx: Context2D, text: String,
  x: Int, y: Int, font: String, 
  alignment: String, baseline: String, color: String
) at io: Unit =
  jsWeb """${ctx}.font = ${font};
           ${ctx}.textAlign = ${alignment};
           ${ctx}.textBaseline = ${baseline};
           ${ctx}.fillStyle = ${color};
           ${ctx}.fillText(${text}, ${x}, ${y})"""

//Sets up the transformation matrix to rotate all later drawn shapes clockwise by 
//the given angle relative to the given coordinates
extern def setupRotation(
  ctx: Context2D, angle: Double, 
  relativeToX: Int, relativeToY: Int
) at io: Unit =
  jsWeb """${ctx}.translate(${relativeToX}, ${relativeToY});
           ${ctx}.rotate(${angle});
           ${ctx}.translate(-${relativeToX}, -${relativeToY});"""

extern type DOMMatrix

extern def getTransform(ctx: Context2D) at io: DOMMatrix = 
  jsWeb "${ctx}.getTransform();"

extern def setTransform(ctx: Context2D, matrix: DOMMatrix) at io: Unit =
  jsWeb "${ctx}.setTransform(${matrix});"

extern type HTMLImage

extern def loadImage(path: String) at io: HTMLImage = 
  jsWeb """(function(){
  image = new Image();
  image.src = ${path};
  return image;
  })()"""

extern def isLoaded(image: HTMLImage) at io: Bool =
  jsWeb "${image}.complete"

extern def drawImage(
  ctx: Context2D, image: HTMLImage,
  dx: Int, dy: Int, 
  dwidth: Int, dheight: Int
) at io: Unit =
  jsWeb "${ctx}.drawImage(${image}, ${dx}, ${dy}, ${dwidth}, ${dheight})"

extern def drawImage(
  ctx: Context2D, image: HTMLImage,
  srcx: Int, srcy: Int,
  srcwidth: Int, srcheight: Int,
  dx: Int, dy: Int, 
  dwidth: Int, dheight: Int
) at io: Unit =
  jsWeb """${ctx}.drawImage(
              ${image}, ${srcx}, ${srcy}, ${srcwidth}, ${srcheight}, 
              ${dx}, ${dy}, ${dwidth}, ${dheight})"""

extern type IdleDeadline

extern def requestIdleCallback(callback: (IdleDeadline) => Unit at {io, global}) at io: Unit =
  jsWeb "window.requestIdleCallback(deadline => $effekt.runToplevel((ks, k) => ${callback}(deadline, ks, k)))"

extern def getTime() at io: Int = jsWeb "Date.now()"

extern def windowWidth() at io: Int = 
  jsWeb "window.innerWidth"

extern def windowHeight() at io: Int =
  jsWeb "window.innerHeight"
