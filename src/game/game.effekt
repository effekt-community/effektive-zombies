module game/game

import src/game/gameobject
import src/game/constants
import src/game/gamestate
import src/game/gametimer
import src/game/player
import src/game/score
import src/ffi
import src/drawable/text
import src/utils
import src/vec2d
import src/drawable/manipulated
import src/drawable/hitbox
import src/drawable/simple

interface Canvas {
  def draw(drawable: ManipulatedDrawable): Unit
  def write(text: Text): Unit
  def clear(): Unit
}

interface Input {
  def isPressed(key: String): Bool
  def mousePos(): Vec2d
}

def visible(d: ManipulatedDrawable): Bool = 
  collides(
    d, Simple(Rect(NULL_VECTOR, GAME_WIDTH.toDouble, GAME_HEIGHT.toDouble, Black()))
  )

def keepInsideScreen(player: Player): Player = {
  val screenW = GAME_WIDTH.toDouble
  val screenH = GAME_HEIGHT.toDouble
  val playerCenter = player.drawable.getCenter()
  val centerInside = Vec2d(
    playerCenter.x.clamp(0.0, screenW),
    playerCenter.y.clamp(0.0, screenH)
  )
  val adjustment = centerInside - playerCenter
  Player(
    player.drawable.move(adjustment), 
    player.gun.move(adjustment), 
    player.health,
    player.speed
  )
}

type Side {
  Top()
  Right()
  Bottom()
  Left()
}

val SIDES = [Top(), Right(), Bottom(), Left()]

/// Returns a zombie randomly with a position 
/// on the outside of one of the screen edges 
/// (such that its barely not visible upon spawn)
def spawnZombie(): GameObject / random = {
  val w = GAME_WIDTH.toDouble
  val h = GAME_HEIGHT.toDouble
  val totalScreenEdge = 2.0 * w + 2.0 * h
  val sideProbs = [w, h, w, h].map {side => side / totalScreenEdge}
  val spawnSide = SIDES.choose(sideProbs).getOrElse{Right()}
  val r = do random()
  val zombiePos = spawnSide match {
    case Right() => Vec2d(w, r * h) + Vec2d(ZOMBIE_WIDTH, 0.0)
    case Bottom() => Vec2d(r * w, h) + Vec2d(0.0, ZOMBIE_HEIGHT)
    case Left() => Vec2d(0.0, r * h) - Vec2d(ZOMBIE_WIDTH, 0.0)
    case Top() => Vec2d(r * w, 0.0) - Vec2d(0.0, ZOMBIE_HEIGHT)
  }
  zombieFactory(zombiePos)
}

/// Create a bullet with a position on the end of the player's pistol barrel
/// if the left mouse button is clicked, else Nil
def spawnBullet(player: Player): Option[GameObject] / Input = {
  if (do isPressed(LEFT_CLICK)) {
    val playerCenter = player.drawable.getCenter()
    val gunAngle = angleClockwise(do mousePos() - playerCenter)
    val bulletDrawable = player.gun.changeColor(Black())
    val bullet = Bullet(
      Rotate(Simple(bulletDrawable), gunAngle, Some(playerCenter)),
      Vec2d(BULLET_SPEED, 0.0).rotate(gunAngle)
    )
    Some(bullet)
  } else None()
}

/// Get the normalized movement vector representing the keyboard inputs of the player
/// Example: W and A are pressed
/// wasdDirection() -> Vec2d(-sqrt(2), -sqrt(2))
def wasdDirection(): Vec2d / Input = {
  val wPressed = do isPressed("w") || do isPressed("W")
  val aPressed = do isPressed("a") || do isPressed("A")
  val sPressed = do isPressed("s") || do isPressed("S")
  val dPressed = do isPressed("d") || do isPressed("D")
  val xDirection = aPressed.toDouble.neg + dPressed.toDouble
  val yDirection = wPressed.toDouble.neg + sPressed.toDouble
  Vec2d(xDirection, yDirection).normalize()
}

def move(player: Player, dt: Int): Player / Input = {
  val movement = 
    player.speed * dt.toDouble * wasdDirection()
  Player(
    player.drawable.move(movement), 
    player.gun.move(movement),
    player.health,
    player.speed
  )
}

def die(gameObject: GameObject): GameObject = gameObject match {
  case Bullet(_, _) => gameObject
  case Zombie(drawable, _, _) => 
    Zombie(
      drawable.setInner(ZOMBIE_DEATH_ANIMATION.wrap.centerAt(drawable.getCenter())), 
      NULL_VECTOR, Dead()
    )
}

/// Returns the remaining objects, the amount of shot zombies 
/// and wether the a bullet missed:
/// - letting zombies colliding with bullets die
/// - removing bullets colliding with alive zombies
/// - removing bullets outside the screen
def shooting(objects: List[GameObject]): (List[GameObject], Int, Bool) = {
  val (inRange, outOfRange) = objects.partition {
    case Bullet(hitbox, _) => hitbox.visible()
    case _ => true
  }

  val (bullets, zombies) = inRange.partition {
    case Bullet(_, _) => true
    case Zombie(_, _, _) => false
  }

  val (bulletsThatHit, remainingBullets) = bullets.partition {
    case Bullet(sHitbox, _) =>
      zombies.any {
        case Zombie(zHitbox, _, Alive()) => sHitbox.collides(zHitbox)
        case _ => false
      }
    case _ => false
  }

  val (dead, alive) = zombies.partition {
    case Zombie(zHitbox, _, Alive()) =>
      bulletsThatHit.any {
        case Bullet(sHitbox, _) => zHitbox.collides(sHitbox)
        case _ => false
      }
    case _              => false
  }

  val died = dead.map {z => z.die()}

  (died.append(alive.append(remainingBullets)), dead.size, outOfRange.nonEmpty)
}

def update(game: GameState, dt: Int): GameState / { Input, random } = game.menu match {
  case Main() => 
    if (do isPressed(LEFT_CLICK) && do mousePos().isInside(PLAY_BUTTON.toHitbox()))
    START_IN_GAME else game
  case Retry() => 
    if (do isPressed(LEFT_CLICK) && do mousePos().isInside(RETRY_BUTTON.toHitbox())) 
    START_IN_GAME else game
  case InGame() =>
    val player = game.player
    val movedPlayer = player.move(dt)
    val objects = game.objects
    val timers = game.timers
    val wave = game.wave
    val score = game.score

    //check if bullet can and should be shot
    val (updatedBulletTimer, shotBullet) = 
      try { 
        val updated = timers.bulletTimer.elapse(dt)
        (updated, None())
      } with elapsed { overflow => 
        val bullet = spawnBullet(player)
        (Timer(if (bullet.isEmpty) 0 else BULLET_INTERVAL), bullet)
      }

    val (remaining, deadZombies, bulletMissed) = 
      shooting(shotBullet.toList.append(objects))

    val (damagingZombies, remainingObjects) = remaining.partition {
      case Zombie(hitbox, _, Alive()) => hitbox.collides(Simple(player.drawable))
      case _ => false
    }

    val playerHealthLoss = damagingZombies.size

    val movedObjects = remainingObjects.map {
      //make zombies chase the player
      case Zombie(h, m, Alive()) =>
        val zombieCenter = h.getCenter()
        val zombiePlayerVec = player.drawable.getCenter() - zombieCenter
        val missingAngle = angleClockwise(zombiePlayerVec) - angleClockwise(m)
        val newHitbox = Rotate(h, missingAngle, None()).collapseRotations()
        Zombie(newHitbox, m.rotate(missingAngle), Alive()).move(dt)
      case o => o.move(dt)
    }

    val (updatedWaveTimer, updatedWave) = 
      try { 
        val updated = timers.waveTimer.elapse(deadZombies)
        (updated, wave)
      } with elapsed { overflow =>
        val newWave = inc(wave); 
        (Timer(START_WAVE_TIMER * newWave - overflow), newWave)
      }

    val (updatedZombieTimer, spawnedZombie) = 
      try { 
          val updated = timers.zombieTimer.elapse(dt)
          (updated, None()) 
      } with elapsed { overflow =>
          (Timer(ZOMBIE_SPAWN_INTERVAL / updatedWave), Some(spawnZombie())) 
      }

    val updatedPlayer = movedPlayer.changeHealth(playerHealthLoss.neg)
                        .keepInsideScreen()
    val updatedObjects = spawnedZombie.toList.append(movedObjects).foldRight(Nil()){
      (o, os) => try { Cons(o.animate(dt), os) } with animationFinished { os }
    }
    val updatedTimers = 
      GameTimer(updatedZombieTimer, updatedWaveTimer, updatedBulletTimer)
    val updatedScore = score.update(playerHealthLoss > 0 || bulletMissed, deadZombies)
    val updatedMenu = if (updatedPlayer.isAlive().not) Retry() else game.menu
    GameState(
      updatedPlayer, 
      updatedObjects, 
      updatedTimers, 
      updatedWave, 
      updatedScore,
      updatedMenu
    )
}

def render(game: GameState): Unit / { Canvas, Input } = game.menu match {
  case Main() => renderMainMenu()
  case InGame() =>
    //Draw the gun of the player according to his rotation 
    val gunAngle = 
      angleClockwise(do mousePos() - game.player.drawable.getCenter())
    do clear()
    do draw(Rotate(
      Compose(Simple(game.player.drawable),
      Simple(game.player.gun)), 
      gunAngle, None()
    ))
    game.objects.foreach { o => do draw(o.getHitbox()) }

    renderUI(game)
  case Retry() => renderDeathScreen(game.score)
}

def renderMainMenu(): Unit / Canvas = {
  do clear()
  do write(EFFEKTIVE_ZOMBIES)
  do draw(Simple(PLAY_BUTTON))
  do write(PLAY_TEXT)
}

def renderUI(game: GameState): Unit / Canvas = {
  val scoreDigits = digits(game.score.score)
  do draw(SCORE_IMAGE.wrap)
  drawDigits(scoreDigits, SCORE_DIGITS, SCORE_POS + fromInt(SCORE_WIDTH, 0))

  val comboDigits = digits(game.score.combo)
  val comboDigitsSize = comboDigits.size
  do draw(COMBO_IMAGE.wrap)
  drawDigits(
    comboDigits, comboDigitsSize, 
    COMBO_POS - fromInt(comboDigitsSize * DIGIT_WIDTH, 0)
  )

  val waveDigits = digits(game.wave)
  do draw(WAVE_IMAGE.wrap)
  drawDigits(waveDigits, WAVE_DIGITS, WAVE_POS + fromInt(WAVE_WIDTH, 0))

  val life = game.player.health
  val hearts = fill(life, HEART_IMAGE)
  hearts.nextToEachOther(LIFE_POS, HEART_PADDING.toDouble)
}

/// Draw the given amount of the digits next to each other from left to right nicely.
/// If more digits should be drawn than in the list, 0's get prepended
def drawDigits(rawDigits: List[Int], howMany: Int, startingWhere: Vec2d): Unit / Canvas = {
  val digits = fill(howMany - rawDigits.size(), 0)
              .append(rawDigits.take(howMany))
              .map{ d => digitImage(d) }
  digits.nextToEachOther(startingWhere, 0.0)
}

/// Draw the given drawables next to each other from left to right,
/// spacing them by their individual width and a specified padding
def nextToEachOther(
  drawables: List[SimpleDrawable],
  from: Vec2d,
  padding: Double
): Unit / Canvas = {
  var pos = from
  drawables.foreach { d =>
    do draw(d.upperLeftAt(pos).wrap)
    pos = pos + Vec2d(d.getWidth() + padding, 0.0)
  }
}

/// digits(420) = List(4, 2, 0)
def digits(n: Int): List[Int] = {
  var remainder = n
  var digits = Nil[Int]()
  while(remainder != 0) {
    val digit = remainder.mod(10)
    digits = Cons(digit, digits)
    remainder = remainder / 10
  }
  if (digits.isEmpty) 0.singleton else digits
}

def renderDeathScreen(score: Score): Unit / Canvas = {
  do draw(DEATH_BACKGROUND)
  do write(YOU_DIED)
  
  val scoreDigits = digits(score.score)
  val maxComboDigits = digits(score.maxCombo).map{digitImage}

  val scoreInfoWidth = SCORE_WIDTH + SCORE_DIGITS * DIGIT_WIDTH
  val comboInfoWidth = COMBO_WIDTH + maxComboDigits.size * DIGIT_WIDTH
  val offset = fromInt((scoreInfoWidth - comboInfoWidth) / 2, 0)

  val realScorePos = RETRY_SCORE_POS + offset
  do draw(SCORE_IMAGE.upperLeftAt(realScorePos).wrap)
  drawDigits(scoreDigits, SCORE_DIGITS, realScorePos + fromInt(SCORE_WIDTH, 0))

  val realComboStartPos = RETRY_MAX_COMBO_START_POS + offset
  nextToEachOther(
    maxComboDigits.append(COMBO_IMAGE.singleton),
    realComboStartPos, 0.0
  )

  do draw(Simple(RETRY_BUTTON))
  do write(RETRY_TEXT)
}