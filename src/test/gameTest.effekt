module gameTest

import test
import mutable/map
import src/game/constants
import src/game/player
import src/game/gametimer
import src/game/game
import src/game/gameobject
import src/game/gamestate
import src/game/score
import src/utils
import src/vec2d
import src/drawable/manipulated
import src/drawable/simple

def gameSuite() = suite("game") {
  val DUMMY_PLAYER = Player(
    Circle(NULL_VECTOR, 0.0, Green()),
    Rect(NULL_VECTOR, 0.0, 0.0, Black()), 0, 0.0
  )
  val DUMMY_TIMER = Timer(0)
  val DUMMY_TIMERS = GameTimer(DUMMY_TIMER, DUMMY_TIMER, DUMMY_TIMER)
  val DUMMY_SCORE = Score(0, 0, 0)
  val DUMMYGAME = 
    GameState(DUMMY_PLAYER, [], DUMMY_TIMERS, 0, DUMMY_SCORE, InGame())

  def pseudoHandler { f: () => Unit / {Input, random} }: Unit = {
    try { f() }
    with Input {
      def isPressed(key) = resume(false)
      def mousePos() = resume(NULL_VECTOR)
    } with random { resume(0.0) }
  }

  test("playerMovement") {
    val keyMap = emptyMap[String, Bool]()
    def assertV(expected: Vec2d) = {
      val calculated = try { wasdDirection() }
      with Input {
        def isPressed(key) = resume(keyMap.get(key).getOrElse { false })
        def mousePos() = resume(NULL_VECTOR)
      }
      val msg = "Map " ++ show(keyMap.keys()) ++ " " ++ show(keyMap.values) ++ ".wasdDirection() = "
                ++ show(calculated) ++ ", but expected " ++ show(expected) 
      assertTrue(calculated == expected, msg)
    }
    assertV(NULL_VECTOR)
    keyMap.update("w", true)
    assertV(Vec2d(0.0, -1.0))
    keyMap.update("s", true)
    assertV(NULL_VECTOR)
    keyMap.update("a", true)
    assertV(Vec2d(-1.0, 0.0))
    keyMap.update("w", false)
    assertV(Vec2d(-1.0 / sqrt(2.0), 1.0 / sqrt(2.0)))
    keyMap.update("s", false)
    assertV(Vec2d(-1.0, 0.0))
    keyMap.update("d", true)
    assertV(NULL_VECTOR)
  }

  test("timer") {
    var timer = Timer(10)
    val nonElapsing = [0, 1, 2, 3]
    val notElapsed = nonElapsing.map { dt => 
      try { 
        timer = timer.elapse(dt) 
        true
      } with elapsed { _ => false }
    }

    assertTrue(notElapsed.all{ x => x })
    assertEqual(timer, Timer(4))

    val elapsing = [4, 5]
    val elapsed = elapsing.map { dt =>
      try {
        timer.elapse(dt)
        false
      } with elapsed { _ => true }
    }

    assertTrue(elapsed.all{ x => x })
  }

  test("score update") {
    val killed3Score = DUMMY_SCORE.update(false, 3)
    val breakScore = killed3Score.update(true, 0)
    val updatedScore = breakScore.update(false, 2)

    assertEqual(updatedScore, Score(9, 2, 3))
  }

  test("zombie shooting") {
    val SHOT_ZOMBIE_HITBOX = Simple(Rect(Vec2d(50.0, 50.0), 10.0, 10.0, Black()))
    val SHOT_ZOMBIE = Zombie(SHOT_ZOMBIE_HITBOX, Vec2d(1.0, 1.0), Alive())

    val ALIVE_HITBOX = Simple(Rect(Vec2d(100.0, 100.0), 10.0, 10.0, Black()))
    val ALIVE = Zombie(ALIVE_HITBOX, Vec2d(1.0, 1.0), Alive())

    val BULLET_HITBOX = Simple(Rect(Vec2d(40.0, 40.0), 20.0, 20.0, Black()))
    val BULLET = Bullet(BULLET_HITBOX, Vec2d(1.0, 1.0))

    val MISSING_HITBOX = Simple(Rect(Vec2d(80.0, 80.0), 10.0, 10.0, Black()))
    val MISSING_BULLET = Bullet(MISSING_HITBOX, Vec2d(3.0, 3.0))

    val OUT_OF_RANGE_HITBOX = 
      Simple(Rect(fromInt(GAME_WIDTH + 10, GAME_HEIGHT + 10), 10.0, 10.0, Black()))
    val OUT_OF_RANGE = Bullet(OUT_OF_RANGE_HITBOX, Vec2d(2.0, 2.0))

    assertTrue(
      SHOT_ZOMBIE_HITBOX.collides(BULLET_HITBOX),
      "Bullet should collide with overlapping zombie"
    )

    assertFalse(
      ALIVE_HITBOX.collides(BULLET_HITBOX),
      "Bullet should not collide with non-overlapping zombie"
    )
    
    val (remaining, dead, missed) = shooting(
      [MISSING_BULLET, SHOT_ZOMBIE, OUT_OF_RANGE, ALIVE, BULLET]
    )

    assert(dead, 1, "Shooting should signal that exactly one zombie died")

    assertTrue(missed, "Shot missed, but function signals the contrary")

    assertFalse(
      remaining.contains(OUT_OF_RANGE){(a, b) => a.equals(b)},
      "Bullet which leaved the screen was not removed"
    )

    assertTrue(
      remaining.contains(MISSING_BULLET){(a, b) => a.equals(b)},
      "Bullet that missed but is inside the screen was removed"
    )

    assertTrue(
      remaining.contains(ALIVE){(a, b) => a.equals(b)},
      "Zombie that was not shot is dead"
    )
    assertFalse(
      remaining.contains(SHOT_ZOMBIE){(a, b) => a.equals(b)},
      "Shot zombie is not dead"
    )

    assertFalse(
      remaining.contains(BULLET){(a, b) => a.equals(b)},
      "Bullet that hit zombie is still in the game"
    )
  }

  test("digits") {
    val digits = digits(420)

    assertEqual(digits, [4, 2, 0])
  }
}